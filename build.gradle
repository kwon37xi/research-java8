ext {
    javaVersion = '1.8'
    slf4jVersion = '1.7.13'
    logbackVersion = '1.2.3'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'

    group 'kr.pe.kwonnam.research-java8'
    version '0.1'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        testCompile "junit:junit:4.12"
        testCompile 'org.mockito:mockito-core:1.10.19'
    }
    compileJava.options.compilerArgs = ['-Xlint:unchecked',"-Xlint:deprecation"]

    jacoco {
        toolVersion = '0.7.5.201505241946'
    }

    test {
        ignoreFailures = true
        testLogging {
            showStandardStreams true
        }
        jacoco {
            enabled = true
        }
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
        ignoreFailures = true
        configFile = rootProject.file('config/checkstyle/checkstyle.xml') // rule 파일
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
        ignoreFailures = true
        effort = 'default'
        reportLevel = 'medium'
    }

    pmd {
        ignoreFailures = true // 위반이 있어도 report만하고 넘어감
    }

}
